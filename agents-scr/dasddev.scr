/**
 * $Id$
 *
 * File:
 *   dasddev.scr
 * Summary:
 *   SCR Agent for reading dasd device information
 * Access:
 *   read-only
 * Authors:
 *   Thomas Fehr <fehr@suse.de>
 * See:
 *   anyagent
 *   libscr
 *   cat /proc/dasd/devices on S390
 * Example:
 *   Read(.proc.dasddev)
 *   ([$["address":"0150", 
 *       "major": 94, 
 *       "minor": 0, 
 *       "name": "dasda", 
 *       "state": "active", 
 *       "blocksize": 4096, 
 *       "blocks": 600660], 
 *    ...
 *   ])
 **
 *
 */
.proc.dasddev

`ag_anyagent(
  Description (
      ``(File("/proc/dasd/devices")),	// real file name
      "#\n",			// Comment
      true,			// read-only
      ``(List (
    	Tuple (
	  Optional(Whitespace()),
    	  `address (String("^( ")),
	  String("^\t "),
	  Whitespace(),
	  "at (",
	  Optional(Whitespace()),
	  `major( Number() ),
	  Optional(Whitespace()),
	  ":",
	  Optional(Whitespace()),
	  `minor( Number() ),
	  Optional(Whitespace()),
	  ")",
	  Optional(Whitespace()),
	  "is",
	  Optional(Whitespace()),
	  `name(String("^: ")),
	  ":",
	  Optional(Whitespace()),
	  `state(String("^ ")),
	  Whitespace(),
	  "at",
	  Whitespace(),
	  "blocksize:",
	  Optional(Whitespace()),
    	  `blocksize( Number() ),
	  Optional(Whitespace()),
	  ",",
	  Optional(Whitespace()),
    	  `blocks( Number() ),
    	  Optional(String("^\n"))
    	),
    	"\n"
      ))
    )
)
