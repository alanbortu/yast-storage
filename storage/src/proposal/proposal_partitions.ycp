/**
 * Module:		proposal_partitions.ycp
 *
 * $Id$
 *
 * Author:		Klaus Kaempf <kkaempf@suse.de>
 *
 * Purpose:		Proposal function dispatcher - partitions.
 *
 *			See also file proposal-API.txt for details.
 */
{
    textdomain "proposal";

    import "Arch";

    include "partitioning/do_proposal.ycp";
    include "partitioning/partition_defines.ycp";
    include "partitioning/p_seq.ycp";

    string func  = Args(0);
    map    param = Args(1);
    map    ret   = $[];

    y2milestone( "func:%1 param:%2", func, param );
    
    if  ( func == "MakeProposal"  && Mode::autoinst) {
	ret["preformatted_proposal"] = 
	    TargetChangesToRichText(Storage::GetTargetMap());
    }
    else if ( func == "MakeProposal" )
    {
	boolean force_reset      = param["force_reset"     ]:false;
	boolean language_changed = param["language_changed"]:false;

	if( force_reset || Storage::GetPartProposalFirst() )
	    {
	    if( !Storage::GetPartProposalFirst() )
		{
		Storage::ResetOndiskTarget();
		}
	    map prop = do_proposal();
	    if( prop["ok"]:false )
		{
		Storage::SetTargetMap( prop["target"]:$[] );
		Storage::SetPartProposalMode( "accept" );
		}
	    else
		{
		Storage::SetPartProposalMode( "impossible" );
		}
	    Storage::SetPartProposalFirst( false );
	    Storage::SetPartProposalActive( true );
	    }

	y2milestone( "prop=%1 force:%2", prop, force_reset );

	if( Storage::GetPartProposalMode() != "impossible" )
	    {
	    ret["preformatted_proposal"] = 
		TargetChangesToRichText(Storage::GetTargetMap());
	    }
	else
	    {
	    ret["raw_proposal"] = [];
	    ret["warning"] = _("No proposal possible.");
	    ret["warning_level"] = `blocker;
	    }
	
    }
    else if ( func == "AskUser" )
    {
	boolean has_next = param["has_next"]:false;

	// call some function that displays a user dialog
	// or a sequence of dialogs here:
	//
	// sequence = DummyMod::AskUser( has_next );

	map aliases = 
	    $[ "disk" : 
		    ``(CallFunction(`inst_disk(true, true))),
	       "target_sel" :
		    ``(CallFunction(`inst_target_selection(true, true))),
	       "target_part" :
		    ``(CallFunction(`inst_target_part(true, true))),
	       "resize_ui" :
		    ``(CallFunction(`inst_resize_ui(true, true))),
	       "part_proposal" :
		    ``(CallFunction(`inst_part_proposal(true, true)))
	     ];

	map seq = $[];

	Storage::SetPartProposalMode( "accept" );

	seq["disk"] = $[ `abort : `abort, `cancel: `cancel, `next: `next ];

	if( Arch::s390)
	    {
	    seq["ws_start"] = "disk";
	    }
	else if (Mode::autoinst) {
	    Storage::SetPartMode("PROP_MODIFY");
	    seq["ws_start"] = "disk";
	}
	else 
	    {
	    seq["target_sel"] = $[ `abort : `abort, `cancel: `cancel,
	                           `next : "target_part" ];
	    seq["target_part"] = $[ `abort : `abort, `cancel: `cancel,
	                           `next : "disk" ];
	    if( Arch::i386 )
		{
		seq["resize_ui"] = $[ `abort : `abort, `cancel: `cancel,
		                      `next : "disk" ];
		seq["target_part",`next] = "resize_ui";
		}
	    seq["disk"] = $[ `abort : `abort, `cancel: `cancel, `next : `next ];
	    if( Storage::GetPartProposalMode() != "impossible" &&
	        Storage::GetPartProposalActive() )
		{
		seq["part_proposal"] = $[ `abort : `abort, `cancel: `cancel, 
		                          `next : "target_sel" ];
		seq["ws_start"] = "part_proposal";
		}
	    else if( Storage::GetPartMode()=="USE_DISK" )
		{
		seq["ws_start"] = "target_sel";
		}
	    else 
		{
		seq["ws_start"] = "disk";
		}
	    }

	Wizard::OpenDialog( Wizard::GenericDialog( Wizard::BackNextButtonBox() ) );
	symbol result = PWizardSequencer(aliases, seq);
	Wizard::CloseDialog();

	y2milestone( "AskUser Mode:%1 ProActive:%2", Storage::GetPartMode(),
	             Storage::GetPartProposalActive() );

	// Fill return map
	
	ret = $[ "workflow_sequence" : result ];
    }
    else if ( func == "Description" )
    {
	// Fill return map.
	//
	// Static values do just nicely here, no need to call a function.
	
	ret =
	    $[
	      "rich_text_title"	:	_("Partitioning"),
	      "menu_title"	:	_("&Partitioning"),
	      "id"		:	"partitions_stuff"
	    ];
    }

    return ret;
}
