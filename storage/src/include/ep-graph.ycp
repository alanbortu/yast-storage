/**
 * File:	ep-graph.ycp
 * Package:	yast2-storage
 * Summary:	Expert Partitioner
 * Authors:	Arvin Schnell <aschnell@suse.de>
 */
{
    textdomain "storage";


    void CreateGraphPanel(any user_data)
    {
	string filename = Directory::tmpdir + "/storage.dot";
	y2milestone("saving graph to %1", filename);
	Storage::SaveGraph(filename);

	UI::ReplaceWidget(`tree_panel,
			  Greasemonkey::Transform(
			      `VBox(
				  // dialog heading, graph is the mathematic term for
				  // a set of notes connected with edges
				  `IconAndHeading(_("Graph"), graph_icon),
				  `Graph(`id(`graph), filename, "dot"),
				  `HBox(
				      // button text
				      `PushButton(`id(`export), _("Export Graph...")),
				      `HStretch()
				      )
				  ))
	    );

	// helptext
	string helptext = _("<p>This view shows a graph.</p>");

	Wizard::RestoreHelp(helptext);
    }


    void HandleGraphPanel(any user_data, symbol widget)
    {
	switch (widget)
	{
	    case `graph:
	    {
		string node = (string) UI::QueryWidget(`id(`graph), `Item);

		if (String::StartsWith(node, "device:"))
		{
		    string device = substring(node, 7);
		    TreePanel::SwitchToNew(device);
		    UI::SetFocus(UI::WidgetExists(`id(`table)) ? `id(`table) : `id(`text));
		}
		else if (String::StartsWith(node, "mountpoint:"))
		{
		    // TODO
		}
	    }
	    break;

	    case `export:
	    {
		string filename = UI::AskForSaveFileName("/tmp", "*.dot", "Save as...");
		if (filename != nil)
		{
		    y2milestone("saving graph to %1", filename);
		    Storage::SaveGraph(filename);
		}
	    }
	    break;
	}
    }
}
