/**
 * File:	ep-tmpfs-lib.ycp
 * Package:	yast2-storage
 * Summary:	Expert Partitioner
 * Authors:	Thomas Fehr <fehr@suse.de>
 *
 * This file must only be included in other Expert Partitioner files ("ep-*.ycp").
 */
{
    textdomain "storage";


void EpDeleteTmpfsDevice(string device)
    {
    if (device == nil)
	{
	// error popup
	Popup::Error(_("No TMPFS device selected."));
	return;
	}

    list<string> tmp = splitstring(device,":");
    string mount = tmp[1]:"";
    y2milestone( "EpDeleteTmpfsDevice device:%1 mount:%2", device, mount );
    // YesNo popup.  %1 is path to a file
    if( Popup::YesNo( sformat(_("\nReally delete tmpfs mounted to %1"), mount )) &&
        Storage::DelTmpfsVolume( mount ))
	{
	any new_focus = nil;
	if (UI::QueryWidget(`tree, `CurrentItem) == device)
	    new_focus = `tmpfs;
	UpdateMainStatus();
	UpdateNavigationTree(new_focus);
	TreePanel::Create();
	}
    }

void EpAddTmpfsDevice()
    {
    map<string, any> data = $[ "device":"tmpfs", "fstype":"TMPFS", "format":true, 
                               "type":`tmpfs, "detected_fs":`tmpfs, "used_fs":`tmpfs ];
    if( DlgCreateTmpfs(data) )
	{
	y2milestone( "EpAddTmpfsDevice data:%1", data );
	Storage::AddTmpfsVolume( data["mount"]:"",  data["fstopt"]:"" );
	UpdateMainStatus();
	UpdateNavigationTree(nil);
	TreePanel::Create();
	}
    }
}
